define is_zeroized_full
    set $addr_to_check = $arg0

    # XXX: $_memeq is broken right now and treats the third argument as the number
    # of 4-byte words instead of bytes.
    if $_memeq($addr_to_check as *u8, $zero, 8)
        printf "✅ Object at addr %p fully zeroized\n", $addr_to_check
        x/32bx $addr_to_check
    else
        printf "❌ Object at addr %p *NOT* fully zeroized\n", $addr_to_check
        x/32bx $addr_to_check
        quit 1
    end
end

document is_zeroized_full
Checks whether the object at $arg0 is fully zeroized (all of its 32 bytes are
zero).
end

# Point B
break vodozemac_zeroization_test::use_keys

run


# We're now at point B.
#
# We save the address of both the original buffer in the frame above and the
# address of the current buffer in `use_keys`.
up
set $addr1 = keys.0.secret_key
set $zero = &_zero

down
set $addr2 = _keys.0.secret_key


# Since we've placed the buffer behind a Box, these addresses should be the
# same. We check and print whether that is the case.

printf "addr1: %p, addr2: %p\n", $addr1, $addr2

if $addr1 != $addr2
    printf "[*] addr1 and addr2 are different objects.\n"
else
    printf "[*] addr1 and addr2 are *the same* object.\n"
end


# Then we ensure the buffer is not already all zeros, since that almost surely
# indicates that something is wrong.

if $_memeq($addr1, $addr2, 32)
    printf "✅ The objects have the same content.\n"
else
    printf "❌ The objects have *different* content.\n"
    quit 1
end

if ! $_memeq($addr1, $zero, 32)
    printf "✅ This content isn't all null bytes.\n"
    x/32bx $addr1
else
    printf "❌ This content *is* all null bytes.\n"
    quit 2
end

break free

# Point C
break 16

# Proceed to the next `free` call. The object should have already been dropped
# and should be fully zeroized (all 32 bytes).
continue

is_zeroized_full $addr1
is_zeroized_full $addr2

# If all was well, we've arrived at this point and the script will finish with
# a success exit code. If something went wrong, we've never reached this point
# and returned a non-zero exit code.
